#! /usr/bin/env perl

use strict;
use warnings;

use FindBin;
use lib "$FindBin::Bin/../perl/lib";

use TeamTools::Otter::Logs;

my $log_finder = TeamTools::Otter::Logs->new;

my $op = 'less';

while (@ARGV) {
    my $sw = shift @ARGV;
    if ($sw =~ m{^(\d+)$}) {
        $log_finder->nth($1);
    } elsif ($sw =~ m{^--(less|grep|ls)$}) {
        $op = $1;
    } elsif ($sw =~ m{^(--help|-h)$}) {
        warn "Syntax: $0 [ --less | --grep | --ls ] [ <username> | <number> ]* [ -- <args for next op> ]

Display Otterlace client logfile for specified user, and/or for the
Nth most recent.

Default is
  0th (most recent) logfile
  belonging to yourself
  sent to less(1)\n";
        exit 2;
    } elsif ($sw eq '--') {
        last;
    } else {
        $log_finder->user($sw);
    }
}

my $logfile = $log_finder->log_file;

my @arg = @ARGV; # leftovers following "--"

# Default args chosen arbitrarily & open for improvement
if (!@arg) {
    @arg = qw( -l ) if $op eq 'ls';
    @arg = qw( -MS ) if $op eq 'less';
    @arg = ('--color=auto', '-E', 'git HEAD|, pid [0-9]*$|Process .* exited|: ===') if $op eq 'grep';
}

my @cmd = ($op, @arg, $logfile);
warn sprintf("Run:  %s\n", join ' ', map {/ / ? qq{'$_'} : $_} @cmd);
exec @cmd;
