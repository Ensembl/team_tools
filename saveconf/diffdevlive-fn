#! /usr/bin/perl

use strict;
use warnings;

=head1 NAME

diffdevlive-fn - extract filenames from diffdevlive summary

=head1 SYNOPSIS

 diffdevlive $FOO/ > out.ddl
 diffdevlive-fn -m7 -0 < out.ddl | xargs -r0 echo Diffs on

=head1 DESCRIPTION

This is a helper filter to extract from the output of "diffdevlive
$DIR" the filenames matching criteria.

=cut


sub main {
    my %opt = (mask => 7, sep => "\n");

    while (@ARGV) {
	$_ = shift @ARGV;
	if (/^-m(\d*)$/) {
	    $opt{mask} = ($1 eq '' ? shift @ARGV : $1);
	    die "Bad -m mask $opt{mask}" unless $opt{mask} =~ /^\d$/;
	} elsif ($_ eq '-0') {
	    $opt{sep} = "\x00";
	} else {
	    die "Unknown option $_";
	}
    }

    $\ = $opt{sep};
    my $goodhead = qr{^ DIFF DEV LIVE Filename$};
    my $prefix = "/nfs/WWWdev/";
    my $seen_gh = 0;
    while (<STDIN>) {
	chomp;
	s/(\x1B\[\d+m)//g; # strip colour escapes
	if (m{^\s+(-|X)\s+(-|X)\s+(-|X)\s+(.*?)\s*$}) {
	    die "Haven't seen $goodhead yet - format changed?" unless $seen_gh;
	    my @bits = ($1, $2, $3);
	    my $fn = $4;
	    my $val = 0;
	    for (my $i=0; $i < @bits; $i++) {
		$val |= 1 << $i if $bits[-$i] eq 'X';
	    }
	    print $prefix.$fn if $val == $opt{mask};
	} elsif ($_ =~ $goodhead) {
	    $seen_gh = 1;
	} elsif (m{^\s*$}) { # blank line
	} elsif (m{^(Webadmin server contacted\.|Cwd:|Opts:|Fn:|Cmd:) }) {
	    # ignore
	} else {
	    die "$0: Unexpected input: $_";
	}
    }
}

main();
